buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.4.34")
    }
}

plugins {
    id 'application'
    id 'uk.gov.hmcts.java' version '0.12.63'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id "info.solidsoft.pitest" version '1.15.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'org.springframework.boot' version '3.3.7'
    id "org.flywaydb.flyway" version '9.22.3'
    id 'au.com.dius.pact' version '4.1.7'// do not change, otherwise serenity report fails
    id 'org.owasp.dependencycheck' version '10.0.3'
}


apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

ext {
    springCloudVersion = '2023.0.4'
}

def versions = [
        lombok          : '1.18.32',
        gradlePitest    : '1.15.0',
        pitest          : '1.16.1',
        sonarPitest     : '0.5',
        junitPitest     : '1.2.1',
        reformLogging   : '6.1.7',
        camel           : '3.8.0',
        pact_version    : '4.1.7',
        launchDarklySdk : '5.10.9',
        junit           : '5.9.2',
        junitPlatform   : '1.10.3',
        log4j           : '2.23.1',
        reformS2sClient : '5.2.0',
        feign           : '3.8.0',
        bytebuddy       : '1.15.3',
        bouncycastle    : '1.78.1',
        postgresql_tc   : '1.20.0'
]

mainClassName = 'uk.gov.hmcts.reform.professionalapi.Application'
def jarName = 'rd-professional-api.jar'

group = 'uk.gov.hmcts.reform.refdata'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }
        }
    }
}

compileJava {

    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {

    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
    maxHeapSize = '2048m'
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }

    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
    pactTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/pactTest/java')
        }
        resources.srcDir file('src/pactTest/resources')
    }

}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestImplementation.extendsFrom testCompile
    functionalTestRuntimeOnly.extendsFrom testRuntime
    contractTestImplementation.extendsFrom testCompile
    contractTestRuntimeOnly.extendsFrom testRuntime
    pactTestImplementation.extendsFrom testCompile
    pactTestRuntime.extendsFrom testRuntime
}

check.dependsOn jacocoTestCoverageVerification


pmd {
    toolVersion = '6.55.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
    maxParallelForks = (Runtime.runtime.availableProcessors().intdiv(2) >= 2) ? 2 : 1
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    finalizedBy aggregate
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    baselineOnMigrate = true
    outOfOrder = true
    locations = ["filesystem:src/main/resources/db/migration", "filesystem:src/main/resources/db/postgres"]
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}


task runProviderPactVerification(type: Test) {
    description = 'Runs the provider Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    if (project.hasProperty('pact.verifier.publishResults')) {
        systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
    }
    systemProperty 'pact.provider.version', project.pactVersion
    include "uk/gov/hmcts/reform/professionalapi/provider/**"
}

task fortifyScan(type: JavaExec) {
    mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
    ignoreExitValue = true
}

runProviderPactVerification.finalizedBy pactVerify

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pactVersion
    }
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

pitest {
    pitestVersion = versions.pitest
    junit5PluginVersion = versions.junitPitest
    targetClasses = ['uk.gov.hmcts.reform.professionalapi.*']
    excludedClasses = [
            'uk.gov.hmcts.reform.professionalapi.configuration.*',
            'uk.gov.hmcts.reform.professionalapi.controller.*',
            'uk.gov.hmcts.reform.professionalapi.Application',
            'uk.gov.hmcts.reform.professionalapi.dataload.config.*',
            'uk.gov.hmcts.reform.professionalapi.dataload.binder.*',
            'uk.gov.hmcts.reform.professionalapi.dataload.route.*',
            'uk.gov.hmcts.reform.professionalapi.dataload.service.dto.*',
            'uk.gov.hmcts.reform.professionalapi.dataload.scheduler.*',
            'uk.gov.hmcts.reform.professionalapi.dataload.processor.JsrValidationBaseProcessor',
            'uk.gov.hmcts.reform.professionalapi.dataload.processor.HeaderValidationProcessor',
            'uk.gov.hmcts.reform.professionalapi.dataload.validator.*'
    ]
    threads = 10
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 85
}


project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "RD :: Professional Reference Data api"
        property "sonar.projectKey", "RDPROFESSIONALAPI"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/professionalapi/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/professionalapi/SystemDateProvider.java," +
                "src/main/java/uk/gov/hmcts/reform/professionalapi/Application.java," +
                "src/main/java/uk/gov/hmcts/reform/professionalapi/util/RefDataUtil.java,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/dataload/config/**,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/dataload/processor/JsrValidationBaseProcessor.java,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/dataload/scheduler/**,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/domain/PrdDataloadSchedulerJob.java,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/util/PrdDataLoadSchedulerAudit.java,"+
                "src/main/java/uk/gov/hmcts/reform/professionalapi/dataload/validator/JsrValidatorInitializer.java"

    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.4'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    implementation group: 'org.springframework.security', name: 'spring-security-core'

    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: versions.bouncycastle
    implementation group: 'org.springframework', name: 'spring-core'
    implementation group: 'org.springframework', name: 'spring-beans'
    implementation group: 'org.springframework', name: 'spring-aop'
    implementation group: 'org.springframework', name: 'spring-aspects'
    implementation group: 'org.springframework', name: 'spring-context'
    implementation group: 'org.springframework', name: 'spring-context-support'
    implementation group: 'org.springframework', name: 'spring-expression'
    implementation group: 'org.springframework', name: 'spring-jcl'
    implementation group: 'org.springframework', name: 'spring-jdbc'
    implementation group: 'org.springframework', name: 'spring-orm'
    implementation group: 'org.springframework', name: 'spring-tx'
    implementation group: 'org.springframework', name: 'spring-web'
    implementation group: 'org.springframework', name: 'spring-webmvc'

    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feign
    implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feign
    implementation "io.github.openfeign:feign-httpclient:11.10"
    implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j

    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '4.0.5'

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.reformS2sClient

    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.18.2', ext: 'pom'
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation 'com.github.hmcts:idam-java-client:2.1.1'
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.41.2'
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //Fix for CVE-2021-29425
    implementation 'commons-io:commons-io:2.8.0'

    implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.18.2'
    runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.18.2'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.4'

    implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
    //Added org.glassfish to support javax.el
    implementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'

    implementation (group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: versions.launchDarklySdk) {
        exclude group: 'com.google.guava', module: 'guava'
    }


    testImplementation group: 'org.apache.camel', name: 'camel-test', version: versions.camel
    testImplementation group: 'org.apache.camel', name: 'camel-test-spring-junit5', version: versions.camel
    testImplementation group: 'org.apache.camel', name: 'camel-test-junit5', version: versions.camel


    implementation group: 'org.seleniumhq.selenium', name: 'selenium-devtools', version: '4.0.0-rc-1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.11.0'
    implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"

    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.8.0'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'

    implementation group: 'org.apache.camel', name: 'camel-csv', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-sql', version: versions.camel
    implementation group: 'org.apache.camel', name: 'camel-spring-batch', version: versions.camel
    implementation group: 'com.opencsv', name: 'opencsv', version: '3.7'
    implementation group: 'org.apache.camel', name: 'camel-bean-validator', version: versions.camel
    implementation group: 'org.apache.camel', name: 'spi-annotations', version: '3.4.1'
    implementation group: 'org.apache.camel.springboot', name: 'camel-spring-boot-starter',  version: '4.2.0'
    implementation group: 'org.apache.camel', name: 'camel-core',  version: '4.2.0'
    implementation group: 'org.apache.camel.springboot', name: 'camel-azure-starter',  version: '3.8.0'
    implementation group: 'org.apache.camel.springboot', name: 'camel-jdbc-starter',  version: '4.2.0'

    implementation group: 'org.apache.camel.springboot', name: 'camel-bindy-starter', version: '4.2.0'
    //Added below dependencies for shedlock
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '5.2.0'
    implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '5.2.0'


    implementation group: 'com.nimbusds', name: 'lang-tag', version: '1.7'
    implementation group: 'org.json', name: 'json', version: '20230227'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    testImplementation 'net.serenity-bdd:serenity-rest-assured:3.8.1'
    testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: '3.8.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

    testImplementation ("org.mockito:mockito-core:4.3.1") {
        exclude group: "net.bytebuddy", module: "byte-buddy"
        exclude group: "net.bytebuddy", module: "byte-buddy-agent"
    }
    testImplementation ("org.mockito:mockito-inline:4.3.1") {
        exclude group: "net.bytebuddy", module: "byte-buddy"
        exclude group: "net.bytebuddy", module: "byte-buddy-agent"
    }
    testImplementation ('com.github.hmcts:rd-commons-lib:v0.0.14'){
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
        exclude group: 'org.apache.groovy', module: 'groovy-json'
        exclude group: 'org.apache.groovy', module: 'groovy'
        exclude group: 'org.apache.groovy', module: 'groovy-xml'
    }
    testImplementation group: 'org.springframework', name: 'spring-test'

    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddy
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: versions.bytebuddy

    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.4.0'

    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'

    testImplementation('com.github.tomakehurst:wiremock:2.35.2') {
        exclude group: 'com.github.jknack'
    }

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }


    testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    testImplementation group: 'org.pitest', name: 'pitest-junit5-plugin', version: versions.junitPitest

    testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }, version: versions.sonarPitest

    testImplementation 'io.github.openfeign:feign-jackson:12.5'
    testImplementation group: 'com.github.mifmif', name: 'generex', version: '1.0.2'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-runtime-attach', version: '3.6.2'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '3.6.2'


    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version

    functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.2'
    functionalTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    functionalTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
    functionalTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

    smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    smokeTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    smokeTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
    smokeTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    integrationTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    integrationTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
    integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    contractTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
    contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
    contractTestImplementation('org.scala-lang:scala-library:2.13.14')


    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    pactTestImplementation sourceSets.main.runtimeClasspath
    pactTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {

    dependencies {

        //CVE-2023-24998
        dependencySet(group: 'commons-fileupload', version: '1.5') {
            entry 'commons-fileupload'
        }

//        Resolves CVE-2023-4586
        dependencySet(group: 'io.netty', version: '4.1.116.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-http'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-transport'
            entry 'netty-transport-classes-epoll'
            entry 'netty-transport-classes-kqueue'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
        }

    }

    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true

bootJar {
    archiveFileName = jarName
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        // Remedy for CVE-2020-8908, ensures launchdarkly uses correct version
        if (details.requested.group == 'com.google.guava'
                && details.requested.name == 'guava') {
            details.useVersion "32.1.1-jre"
        }
    }

    resolutionStrategy.eachDependency { details ->
        // Remedy for CVE-2024-22233 - remove once spring-boot transitively uses version >= 6.1.14
        if (details.requested.group == 'org.springframework'
                && (details.requested.version == '6.1.12' || details.requested.version == '6.1.13')) {
            details.useVersion "6.1.14"
        }
    }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

rootProject.tasks.named("processFunctionalTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
    duplicatesStrategy = 'include'
}

rootProject.tasks.named("processContractTestResources") {
    duplicatesStrategy = 'include'
}
rootProject.tasks.named("bootJar") {
    duplicatesStrategy = 'include'
}
rootProject.tasks.named("distTar") {
    duplicatesStrategy = 'include'
}
rootProject.tasks.named("distZip") {
    duplicatesStrategy = 'include'
}
